# Maintainer: nalla <nalla@hamal.uberspace.de>

pkgname=('git-extra')
_ver_base=1.1
pkgver=1.1.74.1a9278b
pkgrel=1
pkgdesc="Git for Windows extra files"
arch=('i686' 'x86_64')
url="https://github.com/git-for-windows/build-extra"
license=('GPL')
groups=('VCS')
optdepends=('vim' 'filesystem')
install='git-extra.install'
pkgver() {
  printf "%s.%s.%s" "${_ver_base}" "$(git rev-list --count HEAD -- "$startdir")" "$(git rev-parse --short HEAD)"
}

prepare() {
  test $startdir/$pkgname.install -nt $startdir/$pkgname.install.in &&
  test $startdir/$pkgname.install -nt $startdir/resources/gitconfig &&
  test $startdir/$pkgname.install -nt $startdir/resources/gitattributes ||
  sed -e "/^@@GITCONFIG@@$/r $startdir/resources/gitconfig" \
	-e "/^@@GITCONFIG@@$/d" \
	-e "/^@@GITATTRIBUTES@@$/r $startdir/resources/gitattributes" \
	-e "/^@@GITATTRIBUTES@@$/d" \
	<$startdir/$pkgname.install.in >$startdir/$pkgname.install
}

build() {
  export CC=${MINGW_CHOST}-gcc
  export CXX=${MINGW_CHOST}-g++
  export SRCDIR=$startdir/sources
  export BUILDDIR=build-${MINGW_CHOST}
  mkdir -p ${BUILDDIR}
  make -f $startdir/sources/Makefile
}

package() {
  builddir=build-${MINGW_CHOST}
  install -d -m755 $pkgdir/etc/profile.d
  install -d -m755 $pkgdir/etc/post-install
  install -d -m755 $pkgdir/usr/bin
  install -d -m755 $pkgdir/usr/share/git
  install -d -m755 $pkgdir/${MINGW_PREFIX}/bin
  install -m644 $startdir/resources/inputrc $pkgdir/etc
  install -m644 $startdir/resources/vimrc $pkgdir/etc
  install -m755 $startdir/scripts/vi $pkgdir/usr/bin
  install -m755 $startdir/scripts/notepad $pkgdir/usr/bin
  install -m755 $startdir/scripts/wordpad $pkgdir/usr/bin
  install -m755 $builddir/create-shortcut.exe $pkgdir/${MINGW_PREFIX}/bin
  install -m755 $builddir/WhoUses.exe $pkgdir/${MINGW_PREFIX}/bin
  install -m755 $startdir/profile.d/git-prompt.sh $pkgdir/etc/profile.d
  install -m755 $startdir/profile.d/aliases.sh $pkgdir/etc/profile.d
  install -m755 $startdir/profile.d/env.sh $pkgdir/etc/profile.d
  install -m755 $startdir/profile.d/bash_profile.sh $pkgdir/etc/profile.d
  install -m644 $startdir/resources/msys2-32.ico $pkgdir/usr/share/git
  install -m644 $startdir/scripts/99-post-install-cleanup.post \
	$pkgdir/etc/post-install
  install -m755 $startdir/scripts/astextplain $pkgdir/usr/bin
}
